!!!<div class="banner-image"><img alt="Yaksha Programming Language" src="images/yk-banner.png" style="max-width: 100%;" /></div>
---
# Introduction
---
Yaksha â€“ A manual memory managed, compiled to C99, programming language with python like syntax.
;Please note this is an in progress project. Very early days.
---
## Philosophy
---
Language syntax is minimalistic and looks like Python in most cases. Manual memory management is required. 
New developer should be able to pick up full syntax in few hours.
; However, this is not compatible with Python nor a subset of Python.
---
### End Users
---
Yaksha aims to be a simple general purpose language with a minimalistic standard library. One goal is to have easy access to GPU/parallel programming (this is not started yet) using OpenCL.
There will be first class support for Windows. At the moment author plan on using the language (once it reaches usable level) in personal projects.
;This is a single person project, so any advice, pull-requests/issues, fixes to documentation grammar errors, fixes to spelling errors, well wishes, constructive criticism and/or any other kind of help is welcome.
---
## Sample Programs
---
### Factorial
---
```yaksha
import libs.console

def factorial(x: int) -> int:
    if x <= 0:
        return 1
    return x * factorial(x - 1)


def main() -> int:
    a: int = 0
    while a < 10:
        console.cyan("factorial")
        console.red("(")
        print(a)
        console.red(") = ")
        println(factorial(a))
        a = a + 1
    return 0
```
#### Output
!!!<div class="banner-image"><img alt="Factorial 0 - 9 output screenshot" src="images/yk-sample1.png" style="max-width: 100%;" /></div>
;This simple program demonstrate calculating factorial inefficiently using recursion and printing results to console.
---
### Fractal Tree in the Wind
---
!!!<details>
!!!<summary>Click to see code sample</summary>
```yaksha
import raylib as rl
import raylib.utils
import libs.numbers as num
import libs.perlin
import libs.random


class State:
    width: float
    height: float
    angle: float
    frame_count: u64
    blue: rl.Color
    green: rl.Color
    color3: rl.Color


def branch(x: float, y: float, length: float, angle: float, s: State) -> None:
    if length < 4.0f:
        leaf_width: float = random.random_betweenf(1.0f, 3.0f)
        leaf_height: float = random.random_betweenf(3.0f, 6.0f)
        lerped_green: rl.Color = utils.lerp_color(s.green, s.blue, utils.remap(x, 0.0f, s.width, 0.0f, 1.0f) * 2.0f)
        color: rl.Color = utils.lerp_color(lerped_green, s.color3, utils.remap(y, 0.0f, s.height, 0.0f, 1.0f) * 1.5f)
        rl.draw_ellipse(num.f2i(x), num.f2i(y), leaf_height, leaf_width, color)
        return
    wind: float = utils.sin_deg(perlin.noise1df(num.uu2f(s.frame_count) / 50.0f)) * 100.0f * utils.sin_deg(num.uu2f(s.frame_count) / 2.0f)
    next_y: float = y - length * utils.cos_deg(angle)
    next_x: float = x + length * utils.sin_deg(angle)
    thick: float = utils.remap(length, 0.0f, s.height / 4.0f, 2.0f, 6.0f)
    rl.draw_line_ex(rl.vector2(x, y),
                    rl.vector2(next_x, next_y),
                    thick,
                    rl.color(152, 50, 1, 255))
    r1: float = random.random_betweenf(0.3f, 0.9f)
    r2: float = random.random_betweenf(0.5f, 0.8f)
    branch(next_x, next_y, (length * r2), angle + s.angle + wind * 10.0f, s)
    branch(next_x, next_y, (length * r1), angle - s.angle + wind * 10.0f, s)


def update_draw_frame(s: State) -> None:
    rl.clear_background(rl.color(255, 255, 255, 255))
    branch(s.width / 2.0f, s.height, s.height / 4.0f, 0.0f, s)


def main() -> int:
    s: State = State()
    s.angle = random.random_betweenf(30.0f, 45.0f)
    s.width = 600.0f
    s.height = 600.0f
    s.frame_count = num.i2uu(0)
    s.blue = rl.color(0, 255, 214, 255)
    s.green = rl.color(0, 255, 0, 255)
    s.color3 = rl.color(255, 45, 156, 255)
    rl.init_window(num.f2i(s.width), num.f2i(s.height), "Fractal Tree in the Wind")
    rl.set_target_fps(120)
    seed: u64 = random.init_random()
    while not rl.window_should_close():
        rl.begin_drawing()
        update_draw_frame(s)
        rl.draw_fps(0, 0)
        rl.end_drawing()
        s.frame_count = s.frame_count + num.i2uu(1)
        random.set_seed(seed)
    rl.close_window()
    del s
    return 0
```
!!!</details>
#### Output
!!!<div class="banner-image"><img alt="Tree in wind" src="images/yk-sample2.gif" style="max-width: 100%;" /></div>
;Shows a randomly generated tree moving to the wind.
---